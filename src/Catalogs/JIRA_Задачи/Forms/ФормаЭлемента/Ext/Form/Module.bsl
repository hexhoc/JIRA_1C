
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьРеквизитыФормы();
	ПолучитьКомментарииПоЗадаче();
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
	
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы
	
#КонецОбласти

#Область ОбработчикиКомандФормы
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПолучитьКомментарииПоЗадаче()

	Комментарии = JIRA_ВзаимодействиеC_JIRA_КлиентСервер.ПолучитьКомментарииПоЗадаче(Объект.Код);
	
	Для Индекс = 0 По Комментарии.Количество()-1 цикл
		Комментарий = Комментарии[Индекс];
		//Пример создания обычной группы
		ГруппаАвторКомментария = ЭтаФорма.Элементы.Добавить("ГруппаАвторКомментария"+Индекс, Тип("ГруппаФормы"),Элементы.ГруппаКомментарии);
		ГруппаАвторКомментария.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ДатаСоздания = JIRA_ВзаимодействиеC_JIRA_КлиентСервер.ПреобразоватьВДату(Комментарий["created"]);
		ГруппаАвторКомментария.Заголовок = Комментарий["author"]["displayName"] + " добавил(а) комментарий - " + Формат(ДатаСоздания,"ДФ='д/МММ/г ЧЧ:мм:сс'");
		ГруппаАвторКомментария.Отображение = ОтображениеОбычнойГруппы.СильноеВыделение;
		ГруппаАвторКомментария.ОтображатьЗаголовок = Истина; 
		ГруппаАвторКомментария.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
		ГруппаАвторКомментария.РастягиватьПоГоризонтали = Истина;
		ГруппаАвторКомментария.Поведение=ПоведениеОбычнойГруппы.Свертываемая;
		ГруппаАвторКомментария.ОтображениеУправления = ОтображениеУправленияОбычнойГруппы.Картинка;
		ГруппаАвторКомментария.ШрифтЗаголовка = Новый Шрифт(ГруппаАвторКомментария.ШрифтЗаголовка, ,9, Истина);
		ГруппаАвторКомментария.Ширина = 90;
		//Декорация надпись
		НовыйЭлемент = ЭтаФорма.Элементы.Добавить("НадписьКомментарий"+Индекс, Тип("ДекорацияФормы"),ГруппаАвторКомментария);
		НовыйЭлемент.Вид = ВидДекорацииФормы.Надпись;
		НовыйЭлемент.Заголовок = Комментарий["body"];
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыФормы()
	
	ОценкаВремени = ?(Объект.ОценкаВремени = 0, Объект.ПервоначальнаяОценкаВремени,Объект.ОценкаВремени) / 60 / 60;
	ЗатраченоВремени = Объект.ЗатраченоВремени / 60 / 60;
	ОсталосьВремени = ОценкаВремени - ЗатраченоВремени;
	
	ДанныеЗадачи = JIRA_ВзаимодействиеC_JIRA_КлиентСервер.ПолучитьДанныеПоЗадаче(Объект.Код);
	Прогресс = ДанныеЗадачи["fields"]["progress"]["percent"];
	
	ПараметрыПодключения = JIRA_ВзаимодействиеC_JIRA_Сервер.ПолучитьПараметрыПодключенияJIRA();	
	Элементы.ОткрытьЗадачу.Заголовок = ПараметрыПодключения.Протокол + ПараметрыПодключения.АдресСервера + "/browse/"+объект.Код;
	
	КоличествоДнейВСтатусе = ПолучитьКоличествоДнейВСтатусе();
	Элементы.КоличествоДнейВСтатусе.Заголовок = "Заявка находится в этом статусе "+ КоличествоДнейВСтатусе +" дня(ей)";
	
КонецПроцедуры

Функция ПолучитьКоличествоДнейВСтатусе()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	JIRA_Задачи.Ссылка,
		|	JIRA_Задачи.ДатаСоздания
		|ПОМЕСТИТЬ ВТЗадачиВРаботе
		|ИЗ
		|	Справочник.JIRA_Задачи КАК JIRA_Задачи
		|ГДЕ
		|	JIRA_Задачи.Ссылка = &Задача
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СрезИзмененийСтатусов.Задача,
		|	СрезИзмененийСтатусов.НомерЗаписи,
		|	JIRA_ИсторияИзмененияСтатусов.ДатаСобытия,
		|	JIRA_ИсторияИзмененияСтатусов.КонечныйСтатус
		|ПОМЕСТИТЬ ВТИсторияИзмененийСтатусов
		|ИЗ
		|	(ВЫБРАТЬ
		|		JIRA_ИсторияИзмененияСтатусов.Задача КАК Задача,
		|		МАКСИМУМ(JIRA_ИсторияИзмененияСтатусов.НомерЗаписи) КАК НомерЗаписи
		|	ИЗ
		|		РегистрСведений.JIRA_ИсторияИзмененияСтатусов КАК JIRA_ИсторияИзмененияСтатусов
		|	ГДЕ
		|		JIRA_ИсторияИзмененияСтатусов.Задача В
		|				(ВЫБРАТЬ
		|					ВТЗадачиВРаботе.Ссылка
		|				ИЗ
		|					ВТЗадачиВРаботе КАК ВТЗадачиВРаботе)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		JIRA_ИсторияИзмененияСтатусов.Задача) КАК СрезИзмененийСтатусов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.JIRA_ИсторияИзмененияСтатусов КАК JIRA_ИсторияИзмененияСтатусов
		|		ПО СрезИзмененийСтатусов.Задача = JIRA_ИсторияИзмененияСтатусов.Задача
		|			И СрезИзмененийСтатусов.НомерЗаписи = JIRA_ИсторияИзмененияСтатусов.НомерЗаписи
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТЗадачиВРаботе.Ссылка КАК Задача,
		|	ЕСТЬNULL(ВТИсторияИзмененийСтатусов.ДатаСобытия, ВТЗадачиВРаботе.ДатаСоздания) КАК ДатаСобытия,
		|	РАЗНОСТЬДАТ(ЕСТЬNULL(ВТИсторияИзмененийСтатусов.ДатаСобытия, ВТЗадачиВРаботе.ДатаСоздания), &Период, ДЕНЬ) КАК ДнейПребыванияВСтатусе
		|ИЗ
		|	ВТЗадачиВРаботе КАК ВТЗадачиВРаботе
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИсторияИзмененийСтатусов КАК ВТИсторияИзмененийСтатусов
		|		ПО ВТЗадачиВРаботе.Ссылка = ВТИсторияИзмененийСтатусов.Задача";
	
	Запрос.УстановитьПараметр("Задача", Объект.Ссылка);
	Запрос.УстановитьПараметр("Период", ТекущаяДата());
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Если РезультатЗапроса.Следующий() Тогда
		КоличествоДнейВСтатусе = РезультатЗапроса.ДнейПребыванияВСтатусе;
	Иначе		
		КоличествоДнейВСтатусе = 0;
	КонецЕсли;
	
	Возврат КоличествоДнейВСтатусе;
	
КонецФункции // ПолучитьКоличествоДнейВСтатусе()


&НаКлиенте
Процедура ОткрытьЗадачуНажатие(Элемент)
	JIRA_ВзаимодействиеC_JIRA_КлиентСервер.ОткрытьЗадачуВБраузере(Объект.Код);
КонецПроцедуры

&НаКлиенте
Процедура СвязанныеЗадачиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ПараметрыФормы = Новый Структура("Ключ", Элемент.ТекущиеДанные.СвязаннаяЗадача);
	ОткрытьФорму("Справочник.JIRA_Задачи.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
КонецПроцедуры

#КонецОбласти